plugins {
    id 'com.android.library' version '7.3.1' apply false
}

subprojects {
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    ext {
        libraryName = 'SoundTouch JNI'
        libraryVendorName = 'Tianscar'

        libraryVersionCode = 9
        libraryGroupName = 'com.tianscar.soundtouch'
        libraryVersionName = '1.0.7'

        librarySourceCompatibility = JavaVersion.VERSION_1_8
        libraryTargetCompatibility = JavaVersion.VERSION_1_8

        libraryAndroidNamespace = libraryGroupName
        libraryAndroidCompileSdk = 33
        libraryAndroidMinSdk = 16
        libraryAndroidTargetSdk = 33

        libraryDescription = 'JNI bindings for SoundTouch'
    }
    group = libraryGroupName
    version = libraryVersionName
    repositories {
        mavenCentral()
        google()
    }
}

configure ([project(':core'), project(':javase')]) {
    apply plugin: 'java-library'

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    sourceCompatibility = librarySourceCompatibility
    targetCompatibility = libraryTargetCompatibility

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = libraryGroupName
                artifactId = "${rootProject.name}-${project.name}"
                version = libraryVersionName
                from components.java
                pom {
                    name = libraryName
                    description = libraryDescription
                    url = 'https://github.com/Tianscar/soundtouch-jni'
                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://spdx.org/licenses/MIT.html'
                        }
                    }
                    developers {
                        developer {
                            id = 'Tianscar'
                            name = 'Karstian Lee'
                            email = 'tianscar@protonmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com/Tianscar/soundtouch-jni.git'
                        developerConnection = 'scm:git:git@github.com/Tianscar/soundtouch-jni.git'
                        url = 'https://github.com/Tianscar/soundtouch-jni'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "OSSRH"
                if (project.version.toString().endsWith("-SNAPSHOT")) {
                    url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
                } else {
                    url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                }
                credentials {
                    username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                    password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
                }
            }
        }
    }

    signing {
        sign(publishing.publications.mavenJava)
    }

}