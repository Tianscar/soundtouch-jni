plugins {
    id 'com.android.library' version '7.3.1' apply false
}

subprojects {
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    ext {
        libName = 'SoundTouch JNI'
        libVendorName = 'Tianscar'

        libVersionCode = 7
        libGroupName = 'com.tianscar'
        libVersionName = '1.0.5'

        libSourceCompatibility = JavaVersion.VERSION_1_8
        libTargetCompatibility = JavaVersion.VERSION_1_8

        libAndroidNamespace = 'com.tianscar.soundtouch'
        libAndroidCompileSdk = 33
        libAndroidMinSdk = 16
        libAndroidTargetSdk = 33

        libDescription = 'JNI bindings for SoundTouch'
    }
    group = libGroupName
    version = libVersionName
    repositories {
        mavenCentral()
        google()
    }
}

configure ([project(':core'), project(':javase')]) {
    apply plugin: 'java-library'

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    sourceCompatibility = libSourceCompatibility
    targetCompatibility = libTargetCompatibility

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = libGroupName
                artifactId = "${rootProject.name}-${project.name}"
                version = libVersionName
                from components.java
                pom {
                    name = libName
                    description = libDescription
                    url = 'https://github.com/Tianscar/soundtouch-jni'
                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://spdx.org/licenses/MIT.html'
                        }
                    }
                    developers {
                        developer {
                            id = 'Tianscar'
                            name = 'Tiansword Carbonicacid'
                            email = 'tianscar@protonmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com/Tianscar/soundtouch-jni.git'
                        developerConnection = 'scm:git:git@github.com/Tianscar/soundtouch-jni.git'
                        url = 'https://github.com/Tianscar/soundtouch-jni'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "OSSRH"
                if (project.version.toString().endsWith("-SNAPSHOT")) {
                    url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
                } else {
                    url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                }
                credentials {
                    username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                    password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
                }
            }
        }
    }

    signing {
        def signingKey = findProperty("signingKey") ?: System.getenv("GPG_KEY")
        def signingPassword = findProperty("signingPassword") ?:System.getenv("GPG_PASSWORD")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign(publishing.publications.mavenJava)
    }

}